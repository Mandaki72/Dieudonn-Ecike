#TIMEHORIZON
T = 8760;

#NODE LOAD_SHEDDING
// data from Adequacy and flexibility study 2022 - 2032

#PARAMETERS
unit_GW = 0;                                       
energy_electricity = 0;
pre_installed_capacity = 0.128;         // GW(e) in 2022
max_capacity = 0.2;                     // GW(e)
nb_hours = 1;                           // hours
capex_existing = 0;                     // M€/GW(e)
fom_existing = 10;                      // M€/GW(e)/yr
lifetime_existing = 1;                  // years
capex = 0;
fom = 80;                               // M€/GW(e) if 1500 MW < new_capacity < 2000 MW
vom = 2.5;                              // M€/GWh(e)
lifetime = 1;                           // hour
nb_year = T/8760;
// yearly capex
wacc = 0.07;
yearly_capex = capex * wacc / (1 - (1 + wacc)**(-lifetime));                                            // M€/GW(e)/yr
// used to compute the total cost of the system after the optimisation
yearly_capex_existing = capex_existing * wacc / (1 - (1 + wacc)**(-lifetime_existing));                 // M€/GW(e)/yr
yearly_existing_cost = (yearly_capex_existing + fom_existing) * pre_installed_capacity;                 // M€/yr

#VARIABLES
external: load_reduction[T];            // GWh(e)
internal: new_capacity;                 // GW(e)
internal: daily_reduction[365];         // GWh(e)/day

#CONSTRAINTS
new_capacity >= 0;
load_reduction[t] >= 0;
load_reduction[t] <= new_capacity + pre_installed_capacity;
new_capacity + pre_installed_capacity <= max_capacity; 
daily_reduction[(t-mod(t,24))/24] == sum(load_reduction[i] for i in [t:t+23]) where mod(t,24) == 0;  
daily_reduction[i] <= (pre_installed_capacity + new_capacity) * nb_hours for i in [0:364];

#OBJECTIVES
min fix_cost: (yearly_capex + fom) * nb_year * new_capacity ;           // M€	
min var_cost: vom * load_reduction[t];                                  // M€