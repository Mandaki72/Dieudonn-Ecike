#TIMEHORIZON
T = 8760;

#NODE LOAD_SHIFTING
// data from Adequacy and flexibility study 2022 - 2032 of Elia

#PARAMETERS
unit_GWh = 0;                                       
energy_electricity = 0;
max_load_shifting = 1.5;                            // GWh(e)/day
pre_installed_capacity = - max_load_shifting;       // GW(e)
duration_ratio = 1;
vom = 0.1;                                          // M€/GWh(e)

#VARIABLES
internal: state_of_charge[T];                       // GWh(e)
external: load_reduction[T];                        // GWh(e) 
external: load_increase[T];                         // GWh(e) 
internal: daily_reduction[365];                     // GWh(e)/day

#CONSTRAINTS
state_of_charge[t] <= 0;
load_reduction[t] >= 0;
load_increase[t] >= 0;
state_of_charge[i] == 0 for i in [0:24:T-1];
load_reduction[t] <= duration_ratio * max_load_shifting;
load_increase[t] <= duration_ratio * max_load_shifting;
state_of_charge[i+1] == state_of_charge[i] + load_increase[i] -  load_reduction[i] for i in [0:T-2];
state_of_charge[0] == state_of_charge[T-1] + load_increase[T-1] -  load_reduction[T-1];
daily_reduction[(t-mod(t,24))/24] == sum(load_reduction[i] for i in [t:t+23]) where mod(t,24) == 0;
daily_reduction[i] <= max_load_shifting for i in [0:364];
state_of_charge[t] >= pre_installed_capacity;

#OBJECTIVES	
min var_cost: vom * load_reduction[t];              // M€